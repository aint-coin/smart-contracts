{-# STDLIB_VERSION 6 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

let RSAPUBLIC = fromBase64String("base64:MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEA78ZlexLcQwTPFkAO7cVdkUFDh5ykvWjDNYfTdzhfOw4yScG/G+AsSHuFSvuqJe6J0Ltg5hj7D7YV/XEPlbEtm2iVXXX2cgGY3YzSPwPoqB/oyNSM/XNhqwPasfuGFXTgWNJKGR/mIt/LMJYdURuJUyJSH4nPnieebmLLuM965PLetsaAA4BfDhXZfn8i9cBvuTPNavBbPmD7hFR0Qab/Q8J3JIeS7cmN4ruLT04SW5JzkIt36zXtchThCJa6NV4h65c4apgjLdG64yeCzZxNe8jlmise3S5fZZtoIfD0u/LHg8SfVhP7gwomE+H1o+a3Z4ep0egBt1kdIZPzU35bswIDAQAB")

let ATCID = fromBase58String("9QvRoCqAR8TZWFJTRtAjnxxeMj4sY6wKTD22hKDcjhnR")

let SEP = "__"

func generateRandInt (blockId: String, rsaString: String, num: Int) = {
    let rsaSign = fromBase64String(rsaString)
    let rsaSigValid = rsaVerify(SHA256, toBytes(blockId), rsaSign, RSAPUBLIC)
    if (rsaSigValid)
        then {
            let rand = (toInt(sha256(rsaSign)) % num)
            if ((0 > rand))
                then ((-1 * rand) + 1)
                else (rand + 1)
            }
        else throw("Invalid RSA signature")
}

func checkWinner (miner: String) = {
    let winner = getIntegerValue("winner")
    let winnerVal = getStringValue(miner)
    let winnerValArr = split(winnerVal, SEP)
    let start = parseIntValue(winnerValArr[2])
    let end = parseIntValue(winnerValArr[3])

    if (start > winner || end < winner) then
        throw("This miner is not winner.")
    else
        true
}

@Callable(i)
func mineIntent (address: String) = {

    let amount = i.payments[0].amount
    let totalBefore = getIntegerValue("total")
    let totalAfter = totalBefore + amount
    let start = totalBefore + 1
    let end = totalBefore + amount
    let minerValue = "%d%d%d" + SEP + toString(amount) + SEP + toString(start) + SEP + toString(end)
    
    # let txid = if (totalBefore == 0) then i.transactionId.toBase58String() else getStringValue("txid")
    let txid = i.transactionId.toBase58String()

    let execute = if (amount > 0) then
    [
        StringEntry("miner" + SEP + address, minerValue),
        IntegerEntry("total", totalAfter),
        StringEntry("blockId",  txid)
    ]
    else
        []

    execute
}

@Callable(i)
func mineWinner (blockId: String, rsaSign: String) = {

    let blockIdSaved = getStringValue("blockId")
    if (blockIdSaved != blockId) then throw("Wrong block ID.") else
    let total = getIntegerValue("total")

    if (total > 0) then
        let winner = generateRandInt(blockId, rsaSign, total)
        [
            IntegerEntry("winner", winner)
        ]
    else
        []
}

@Callable(i)
func mineExecute (miner: String) = {
    let winner = getIntegerValue("winner")
    let winnerVal = getStringValue(miner)
    let winnerValArr = split(winnerVal, SEP)
    let winnerKeyArr = split(miner, SEP)
    let winnerAddr = addressFromStringValue(winnerKeyArr[1])

    let amount = 5000000000
    let lb = getIntegerValue("lastBlock")
    let block = getIntegerValue("block")
    let blockIncr = block + 1

    let newBlock = [
        IntegerEntry("lastBlock", height),
        IntegerEntry("block", blockIncr),

        Reissue(ATCID, amount, true),
        ScriptTransfer(winnerAddr, amount, ATCID),
        IntegerEntry("total", 0),
        IntegerEntry("winner", 0)
    ]

    let execute = if (height % 10 == 0 && height != lb && checkWinner(miner)) then newBlock else
        []

    execute
}

@Callable(i)
func mineDelete(miner: String) = {

    [
        DeleteEntry(miner)
    ]
}